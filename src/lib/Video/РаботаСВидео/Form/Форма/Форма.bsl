
&НаКлиенте
Функция СоздатьКартинкуИзТекста(ПараметрыВидео,Текст,ТипВыводаТекста,pointsize)
	КартинкаТекст     = ПолучитьИмявременногоФайла("png");
	ФайлКартинкаТекст = Новый Файл(КартинкаТекст);
	КартинкаТекст     = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + ФайлКартинкаТекст.Имя;
	
	ЛогФайл       = ПолучитьИмяВременногоФайла("txt");
	
	Команда = """" + ПараметрыВидео.ЗаписьВидеоКомандаConvert + """ -gravity Center  -background black  -fill white   -pointsize " + pointsize + " -size <screenwidth>x<screenheight>   " + ТипВыводаТекста + ":""<Text>"" " + КартинкаТекст;
	Команда = СтрЗаменить(Команда,"<screenwidth>",ПараметрыВидео.ЗаписьВидеоЭкранШирина);
	Команда = СтрЗаменить(Команда,"<screenheight>",ПараметрыВидео.ЗаписьВидеоЭкранВысота);
	Команда = СтрЗаменить(Команда,"<Text>",Текст);
	
	Команда = СтрЗаменить(Команда,Символы.НПП,"");
	
	ПараметрыВидео.Ванесса.Отладка("СоздатьКартинкуИзТекста");
	ПараметрыВидео.Ванесса.Отладка(Команда);
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("cmd");
	ЗТ = Новый ЗаписьТекста(ИмяВременногоФайла,"UTF-8",,Истина); 
	ЗТ.ЗаписатьСтроку("CHCP 65001"); 
	ЗТ.ЗаписатьСтроку(""+ Команда); 
	ЗТ.Закрыть();

	КомандаСистемы(ИмяВременногоФайла);
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяВременногоФайла);
	
	Возврат КартинкаТекст;
КонецФункции	

&НаКлиенте
Функция СоздатьВидеоНужнойДлительностиИзКартинки(ПараметрыВидео,КартинкаТекст,ДлительностьВидео)
	КартинкаВидео     = ПолучитьИмявременногоФайла("mp4");
	ФайлКартинкаВидео = Новый Файл(КартинкаВидео);
	КартинкаВидео     = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + ФайлКартинкаВидео.Имя;
	
	Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -loop 1 -i """ + КартинкаТекст + """ -qscale 1 -r 29.97 -t " + ДлительностьВидео + " -y -f mpegts """ + КартинкаВидео + """";
	ПараметрыВидео.Ванесса.Отладка(Команда);
	КомандаСистемы(Команда);
	
	
	
	КартинкаВидеоПереход1     = ПолучитьИмявременногоФайла("mpg");
	ФайлКартинкаВидеоПереход1 = Новый Файл(КартинкаВидеоПереход1);
	КартинкаВидеоПереход1     = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + ФайлКартинкаВидеоПереход1.Имя;
	Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -i " + КартинкаВидео + " -y -qscale 1 -vf fade=in:0:30 " + КартинкаВидеоПереход1;
	ПараметрыВидео.Ванесса.Отладка(Команда);
	КомандаСистемы(Команда);
	
	
	
	КартинкаВидеоПереход2     = ПолучитьИмявременногоФайла("mpg");
	ФайлКартинкаВидеоПереход2 = Новый Файл(КартинкаВидеоПереход2);
	КартинкаВидеоПереход2     = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + ФайлКартинкаВидеоПереход2.Имя;
	
	
	Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -i " + КартинкаВидеоПереход1 + " -y -qscale 1 -vf fade=out:" + ((ДлительностьВидео-1)*30) +":30 " + КартинкаВидеоПереход2;
	ПараметрыВидео.Ванесса.Отладка(Команда);
	КомандаСистемы(Команда);
	
	
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(КартинкаВидео);
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(КартинкаВидеоПереход1);
	
	
	Возврат КартинкаВидеоПереход2;
	
КонецФункции	

&НаКлиенте
Функция ОпределитьДлительностьФайла(ПараметрыВидео,ИмяИсходногоФайла)
	ИмяВременногоФайлаЛог = ПолучитьИмяВременногоФайла("txt");
	Команда = """" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + """ -i " + ИмяИсходногоФайла + " > " + ИмяВременногоФайлаЛог + " 2>&1";
	
	КомандаСистемы(Команда);
	
	
	Текст = Новый ЧтениеТекста;
	Текст.Открыть(ИмяВременногоФайлаЛог,"UTF-8");
	
	Зн = Неопределено;
	Пока Истина Цикл
		Стр = Текст.ПрочитатьСтроку();
		Если Стр = Неопределено Тогда
			Прервать;
		КонецЕсли;	 
		
		Если Найти(Стр,"Duration:") = 0 Тогда
			Продолжить;
		КонецЕсли;	 
		
		
		
		Стр = СокрЛП(СтрЗаменить(Стр,"Duration:",""));
		//Сообщить("Стр=" + Стр);
		Поз = Найти(Стр,",");
		Стр = Лев(Стр,Поз-1);
		
		Зн = Стр;
		Прервать;
	КонецЦикла;	
	
	Текст.Закрыть();
	
	
	Если Зн = Неопределено Тогда
		ВызватьИсключение "Не смог определить длительность у файла: " + ИмяИсходногоФайла;
	КонецЕсли;	 
	
	//Сообщить("Зн="+Зн);
	КолМилиСек = Сред(Зн,10);
	//Сообщить("КолМилиСек="+КолМилиСек);
	Зн = Лев(Зн,8);
	МассивСтрок = РазложитьСтрокуВМассивПодстрок(Зн,":");
	КолСек = 0;
	КолСек = КолСек + Число(МассивСтрок[1])*60 + Число(МассивСтрок[2]) + Число(КолМилиСек)/1000;
	
	КолСек = Окр(КолСек,2);
	//Сообщить("КолСек="+КолСек);
	Возврат КолСек;
КонецФункции	


&НаКлиенте
Процедура ДобавитьMp3КВидео(ПараметрыВидео,ИмяФайлаВидео,Mp3)
	НовоеИмяФайлаВидео     = ПолучитьИмявременногоФайла("mp4");
	ФайлНовоеИмяФайлаВидео = Новый Файл(НовоеИмяФайлаВидео);
	НовоеИмяФайлаВидео     = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + ФайлНовоеИмяФайлаВидео.Имя;
	
	Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -i " + ИмяФайлаВидео + " -i " + Mp3 + " -codec copy -shortest " + НовоеИмяФайлаВидео;
	
	ПараметрыВидео.Ванесса.Отладка(Команда);
	КомандаСистемы(Команда);
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяФайлаВидео);
	ИмяФайлаВидео = НовоеИмяФайлаВидео; 
	
КонецПроцедуры

&НаКлиенте
Функция СоздатьИмяФайлаТишины(ПараметрыВидео,Длительность,ПереданноеИмяФайла)
	КаталогДляФайловTTS = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\TTS";
	ИмяВременногоФайлаMp3 = КаталогДляФайловTTS + "\" + ПереданноеИмяФайла + ".mp3";
	
	Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -f lavfi -i anullsrc=r=44100:cl=stereo -t " + Длительность + " -ab 192k -acodec libmp3lame " + ИмяВременногоФайлаMp3;
	ПараметрыВидео.Ванесса.Отладка(Команда);
	КомандаСистемы(Команда);
	
	Возврат ИмяВременногоФайлаMp3;
КонецФункции	

&НаКлиенте
Функция ОбъединитьФайлыMP3(ПараметрыВидео,КаталогРаботы,Файл1,Файл2,Файл3)
	ИмяФайлаСписок = КаталогРаботы + "\listmp3.txt";
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяФайлаСписок);
	ЗТ = Новый ЗаписьТекста(ИмяФайлаСписок,"windows-1251",,Ложь); 
	
	
	ПромФайл1 = Новый Файл(Файл1);
	ПромФайл2 = Новый Файл(Файл2);
	ЗТ.ЗаписатьСтроку("file '" + ПромФайл1.Имя + "'"); 
	ЗТ.ЗаписатьСтроку("file '" + ПромФайл2.Имя + "'"); 
	Если Файл3 <> Неопределено Тогда
		ПромФайл3 = Новый Файл(Файл3);
		ЗТ.ЗаписатьСтроку("file '" + ПромФайл3.Имя + "'"); 
	КонецЕсли;	 
	
	ЗТ.Закрыть();
	
	
	
	ВременныйMp3 = ПолучитьИмяВременногоФайла("mp3");
	ФайлВременныйMp3 = Новый Файл(ВременныйMp3);
	ВременныйMp3 = КаталогРаботы + "\" + ФайлВременныйMp3.Имя;
	ФайлВременныйMp3 = Новый Файл(ВременныйMp3);
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("cmd");
	ЗТ = Новый ЗаписьТекста(ИмяВременногоФайла,"UTF-8",,Истина); 
	Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -f concat -i listmp3.txt -c copy " + ФайлВременныйMp3.Имя;
	Сообщить(""+Команда);
	
	ЗТ.ЗаписатьСтроку("CD " + КаталогРаботы); 
	ЗТ.ЗаписатьСтроку(""+ Команда); 
	ЗТ.Закрыть();
	
	retCode = -1;
	ЗапуститьПриложение(ИмяВременногоФайла,,Истина,retCode);
	Если retCode <> 0 Тогда
		ВызватьИсключение("Команда по объединению mp3 не выполнена! Файл1=" + Файл1 + ", Файл2=" + Файл2 + ", Файл3=" + Файл3);
	КонецЕсли;	 
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(Файл1);
	КопироватьФайл(ВременныйMp3,Файл1);
	
	Возврат Файл1;
КонецФункции	

&НаКлиенте
Функция СоздатьФайлВидеоИзТекста(ПараметрыВидео,Текст,MP3,ФинальноеИмяФайла)
	КартинкаТекст = СоздатьКартинкуИзТекста(ПараметрыВидео,Текст,"caption",80);
	
	ДлительностьВидео = 5;
	Если MP3 <> Неопределено Тогда
		ДлительностьMp3 = Окр(ОпределитьДлительностьФайла(ПараметрыВидео,MP3));
		Если  ДлительностьВидео < (ДлительностьMp3+2) Тогда
			ДлительностьВидео = ДлительностьMp3+2;
		КонецЕсли;	 
	КонецЕсли;	 
	
	ФайлВидео     = СоздатьВидеоНужнойДлительностиИзКартинки(ПараметрыВидео,КартинкаТекст,ДлительностьВидео);
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(КартинкаТекст);
	
	
	Если Mp3 <> Неопределено Тогда
		ФайлТишины1 = СоздатьИмяФайлаТишины(ПараметрыВидео,1,ФинальноеИмяФайла + "_silence_1");
		ФайлТишины2 = СоздатьИмяФайлаТишины(ПараметрыВидео,10,ФинальноеИмяФайла +"_silence_2");
		ФайлMp3     = ОбъединитьФайлыMP3(ПараметрыВидео,ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\TTS",ФайлТишины1,Mp3,ФайлТишины2);
		ДобавитьMp3КВидео(ПараметрыВидео,ФайлВидео,ФайлMp3)
	КонецЕсли;	 
	
	
	ФайлФайлВидео = Новый Файл(ФайлВидео);
	ПараметрыВидео.Ванесса.ПереместитьФайлКомандаСистемы(ФайлВидео,ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + ФинальноеИмяФайла + ФайлФайлВидео.Расширение);
	
	
	Возврат ФайлВидео;
КонецФункции	

&НаКлиенте
Процедура СоздатьЗаголовокСценария(ПараметрыВидео,ИмяСценария,НомерСценария)
	Текст   = ИмяСценария;
	Текст   = СтрЗаменить(Текст,"<","");
	Текст   = СтрЗаменить(Текст,">","");
	
	СценарийMP3 = ПолучитьФайлMP3ИзТекста(ПараметрыВидео,"Сценарий. " + Текст,"scen_begin_" + ДобавитьНулей(НомерСценария,3));
	
	Текст   = "Сценарий\n\n " + ИмяСценария;
	
	ФайлВидео   = СоздатьФайлВидеоИзТекста(ПараметрыВидео,Текст,СценарийMP3,"scen_begin_" + ДобавитьНулей(НомерСценария,3));
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКрасивыйТег(ЧтоОборачиваем,Стр)
	ЧтоОборачиваемОриг = ЧтоОборачиваем;
	
	ЧтоОборачиваем = "<span underline='single'><b>" + ЧтоОборачиваем + "</b></span>";
	
	Стр = ЧтоОборачиваем + Сред(Стр,СтрДлина(ЧтоОборачиваемОриг)+1);
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСтрокаВыводаФичи(ПараметрыВидео,ФайлФичи,СтрокаMP3)
	Текст = Новый ЧтениеТекста;
	Текст.Открыть(ФайлФичи,"UTF-8");
	
	СтрокаВыводаФичи = "";
	СтрокаMP3 = "";
	Пока Истина Цикл
		Стр = Текст.ПрочитатьСтроку();
		Если Стр = Неопределено Тогда
			Прервать;
		КонецЕсли;	 
		
		Стр = СокрЛП(Стр);
		
		Если Лев(Стр,1) = "#" Тогда
			Продолжить;
		КонецЕсли;	 
		Если Лев(Стр,1) = "@" Тогда
			Продолжить;
		КонецЕсли;	 
		Если Стр = "" Тогда
			Продолжить;
		КонецЕсли;	 
		
		НРегСтр = НРег(Стр);
		
		Если Найти(НРегСтр,"контекст:") > 0 Тогда
			Прервать;
		КонецЕсли;	 
		Если Найти(НРегСтр,"сценарий:") > 0 Тогда
			Прервать;
		КонецЕсли;	 
		
		
		Стр = СтрЗаменить(Стр,">","");
		Стр = СтрЗаменить(Стр,"<","");
		
		СтрокаMP3 = СтрокаMP3 + Стр + Символы.ПС;
		
		Если Лев(НРегСтр,11) = "функционал:" Тогда
			ДобавитьКрасивыйТег(Лев(Стр,11),Стр);
		КонецЕсли;	 
		Если Лев(НРегСтр,3) = "как" Тогда
			ДобавитьКрасивыйТег(Лев(Стр,3),Стр);
		КонецЕсли;	 
		Если Лев(НРегСтр,6) = "я хочу" Тогда
			ДобавитьКрасивыйТег(Лев(Стр,6),Стр);
		КонецЕсли;	 
		Если Лев(НРегСтр,5) = "чтобы" Тогда
			ДобавитьКрасивыйТег(Лев(Стр,5),Стр);
		КонецЕсли;	 
		
		
		Если Лев(НРегСтр,3) = "как" Тогда
			СтрокаВыводаФичи = СтрокаВыводаФичи + Символы.ПС;
		КонецЕсли;	 
		
		Если СтрокаВыводаФичи <> "" Тогда
			СтрокаВыводаФичи = СтрокаВыводаФичи + Символы.ПС;
		КонецЕсли;	 
		СтрокаВыводаФичи = СтрокаВыводаФичи + Стр;
	КонецЦикла;	
	
	Текст.Закрыть();
	
	СтрокаВыводаФичи = СтрЗаменить(СтрокаВыводаФичи,Символы.ПС,"\n");
	
	
	
	СтрокаВыводаФичи = "\n\n\n\n\n" + СтрокаВыводаФичи;
	
	ПараметрыВидео.Ванесса.Отладка("СтрокаВыводаФичи:");
	ПараметрыВидео.Ванесса.Отладка(СтрокаВыводаФичи);
	
	Возврат СтрокаВыводаФичи;
КонецФункции	

&НаКлиенте
Процедура СоздатьIntro(ПараметрыВидео)
	ПолныйПутьКФиче = ПараметрыВидео.МассивСценариевДляВыполнения[0].ПолныйПутьКФиче;
	
	СтрокаMP3 = "";
	СтрокаВыводаФичи = ПолучитьСтрокаВыводаФичи(ПараметрыВидео,ПолныйПутьКФиче,СтрокаMP3);
	//Сообщить("СтрокаВыводаФичи="+СтрокаВыводаФичи);
	
	
	ФайлMp3 = ПолучитьФайлMP3ИзТекста(ПараметрыВидео,СтрокаMP3,"intro");
	ДлительностьMp3 = Цел(ОпределитьДлительностьФайла(ПараметрыВидео,ФайлMp3)) + 1;
	
	
	ДлительностьВидео = 6;
	Если ДлительностьВидео < (ДлительностьMp3+2) Тогда
		ДлительностьВидео = ДлительностьMp3 + 2;
	КонецЕсли;	 
	
	
	КартинкаТекст = СоздатьКартинкуИзТекста(ПараметрыВидео,СтрокаВыводаФичи,"pango",35);
	ФайлВидео     = СоздатьВидеоНужнойДлительностиИзКартинки(ПараметрыВидео,КартинкаТекст,ДлительностьВидео);
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(КартинкаТекст);
	
	
	
	ФайлТишины1 = СоздатьИмяФайлаТишины(ПараметрыВидео,1,"intro_silence_1");
	ФайлТишины2 = СоздатьИмяФайлаТишины(ПараметрыВидео,20,"intro_silence_2");
	ФайлMp3     = ОбъединитьФайлыMP3(ПараметрыВидео,ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\TTS",ФайлТишины1,ФайлMp3,ФайлТишины2);
	
	ДобавитьMp3КВидео(ПараметрыВидео,ФайлВидео,ФайлMp3);
	ФайлФайлВидео = Новый Файл(ФайлВидео);
	ПараметрыВидео.Ванесса.ПереместитьФайлКомандаСистемы(ФайлВидео,ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + "intro" + ФайлФайлВидео.Расширение);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьКоличествоШагов(МассивСценариевДляВыполнения)
	Кол = 0;
	Для каждого Сценарий Из МассивСценариевДляВыполнения Цикл
		Шаги = Сценарий.Шаги;
		Кол  = Кол + Шаги.Количество();
	КонецЦикла;
	
	Возврат Кол;
КонецФункции	

&НаКлиенте
Процедура СоздатьOutro(ПараметрыВидео)
	ПолныйПутьКФиче = ПараметрыВидео.МассивСценариевДляВыполнения[0].ПолныйПутьКФиче;
	
	КолШагов = ПолучитьКоличествоШагов(ПараметрыВидео.МассивСценариевДляВыполнения);
	
	СтрокаMP3 = "";
	АудиоТрек = "";
	СтрокаВыводаФичи = "Видео создано автоматически\n" + ТекущаяДата() + "\n\nКоличество сценариев: " + ПараметрыВидео.МассивСценариевДляВыполнения.Количество() + "\nКоличество шагов: "+КолШагов + "\nВерсия Vanessa-Behavior: " + СтрЗаменить(ПараметрыВидео.Ванесса.Заголовок,"ver","") + "\nВерсия скрипта сборки видео: " + "1.0" + "\nАудио трек: " + АудиоТрек + "\nГолос: Microsoft Speech Platform - " + ПараметрыВидео.ЗаписьВидеоИмяTTS;
	//Сообщить("СтрокаВыводаФичи="+СтрокаВыводаФичи);
	
	
	КартинкаТекст = СоздатьКартинкуИзТекста(ПараметрыВидео,СтрокаВыводаФичи,"caption",70);
	ФайлВидео     = СоздатьВидеоНужнойДлительностиИзКартинки(ПараметрыВидео,КартинкаТекст,5);
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(КартинкаТекст);
	
	ФайлТишины = СоздатьИмяФайлаТишины(ПараметрыВидео,10,"outro_silence");
	ДобавитьMp3КВидео(ПараметрыВидео,ФайлВидео,ФайлТишины);
	
	ФайлФайлВидео = Новый Файл(ФайлВидео);
	ПараметрыВидео.Ванесса.ПереместитьФайлКомандаСистемы(ФайлВидео,ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + "outro" + ФайлФайлВидео.Расширение);
	
	
	
КонецПроцедуры

&НаКлиенте
Процедура УскоритьЗамедлитьФайлВидеоИДобавитьMp3(ПараметрыВидео,ИмяФайлВидео,ИмяФайлMP3,ДлительностьВидео,НоваяДлительностьВидео,ДлительностьMP3)
	ИмяФайлаВидеоВременный     = ПолучитьИмявременногоФайла("mp4");
	ФайлИмяФайлаВидеоВременный = Новый Файл(ИмяФайлаВидеоВременный);
	ИмяФайлаВидеоВременный     = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + ФайлИмяФайлаВидеоВременный.Имя;
	
	Доля    = НоваяДлительностьВидео/ДлительностьВидео;
	Если Доля > 1 Тогда 
		//значит замедляем
		Доля    = Окр(НоваяДлительностьВидео/ДлительностьВидео,1);
	Иначе	
		//значит ускоряем и доля будет что-то вроде 0.425
		Доля    = Окр(НоваяДлительностьВидео/ДлительностьВидео,3);
		Если Доля < 0.3 Тогда
			Доля = 0.3;//чтобы не ускорять слишком быстро
		КонецЕсли;	 
	КонецЕсли;	 
	ДоляСтр = СтрЗаменить(СтрЗаменить(Строка(Доля),",","."),Символы.НПП,"");
	Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -i " + ИмяФайлВидео + " -filter:v ""setpts=" + ДоляСтр + "*PTS"" " + ИмяФайлаВидеоВременный;
	
	
	Если НоваяДлительностьВидео = -1 Тогда //значит ничего делать не надо
		ПараметрыВидео.Ванесса.Отладка("Не стал изменять длительность видео " + ИмяФайлВидео);
		ИмяФайлаВидеоВременный = ИмяФайлВидео;
	Иначе	
		ПараметрыВидео.Ванесса.Отладка(Команда);
		КомандаСистемы(Команда);
	КонецЕсли;	 
	
	//ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяФайлВидео);
	//ПараметрыВидео.Ванесса.ПереместитьФайлКомандаСистемы(ИмяФайлаВидеоВременный,ИмяФайлВидео);

	//добавим звук
	Если ПараметрыВидео.ЗаписьВидеоДелатьНаложениеTTS Тогда
		ФайлИмяФайлMP3     = Новый Файл(ИмяФайлMP3);
		ИмяБезРасширения = ФайлИмяФайлMP3.ИмяБезРасширения;
		
		ФайлТишины1 = СоздатьИмяФайлаТишины(ПараметрыВидео,1,ИмяБезРасширения + "_silence_1");
		ФайлТишины2 = СоздатьИмяФайлаТишины(ПараметрыВидео,10 + Цел(ОпределитьДлительностьФайла(ПараметрыВидео,ИмяФайлаВидеоВременный)),ИмяБезРасширения +"_silence_2");
		ФайлMp3     = ОбъединитьФайлыMP3(ПараметрыВидео,ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\TTS",ФайлТишины1,ФайлИмяФайлMP3.Имя,ФайлТишины2);
		ДобавитьMp3КВидео(ПараметрыВидео,ИмяФайлаВидеоВременный,ФайлMp3)
	Иначе
		ВызватьИсключение "Надо сделать ветку кода!";
	КонецЕсли;	 
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяФайлВидео);
	ПараметрыВидео.Ванесса.ПереместитьФайлКомандаСистемы(ИмяФайлаВидеоВременный,ИмяФайлВидео);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСубтитрыКВидео(ПараметрыВидео,ИмяФайлаШага,Шаг,ДлительностьMP3)
	ИмяФайлаСубтитров = СтрЗаменить(ИмяФайлаШага,".mp4",".srt");
	
	//ДлительностьВидео = Цел(ОпределитьДлительностьФайла(ПараметрыВидео,ИмяФайлаШага));
	ДлительностьТитра = ДлительностьMP3 + 1;
	Если ДлительностьТитра > 59 Тогда
		ДлительностьТитра = 59;
	КонецЕсли;	 
	
	ДлительностьТитраСтр = Строка(ДлительностьТитра);
	Если СтрДлина(ДлительностьТитраСтр) = 1 Тогда
		ДлительностьТитраСтр = "0" + ДлительностьТитраСтр;
	КонецЕсли;	 
	
	ЗТ = Новый ЗаписьТекста(ИмяФайлаСубтитров,"UTF-8",,Ложь); 
	ЗТ.ЗаписатьСтроку("1"); 
	ЗТ.ЗаписатьСтроку("00:00:00,500 --> 00:00:" + ДлительностьТитраСтр + ",500"); 
	ЗТ.ЗаписатьСтроку(Шаг.Имя); 
	ЗТ.Закрыть();
	
	
	ВременныйФайлВидео     = ПолучитьИмявременногоФайла("mp4");
	ФайлВременныйФайлВидео = Новый Файл(ВременныйФайлВидео);
	ВременныйФайлВидео     = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + ФайлВременныйФайлВидео.Имя;
	
	
	ФайлСабов = Новый Файл(ИмяФайлаСубтитров);
	
	Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -i " + ИмяФайлаШага + " -r 29.97 -qscale 1 -vf subtitles=" + ФайлСабов.Имя + ":force_style=""FontName=Consolas"" " + ВременныйФайлВидео;
	ПараметрыВидео.Ванесса.Отладка(Команда);
	//КомандаСистемы(Команда);
	
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("cmd");
	
	ЗТ = Новый ЗаписьТекста(ИмяВременногоФайла,"UTF-8",,Истина); 
	ЗТ.ЗаписатьСтроку("cd " + ФайлСабов.Путь); 
	ЗТ.ЗаписатьСтроку(""+ Команда); 
	ЗТ.Закрыть();
	
	retCode = -1;
	ЗапуститьПриложение(ИмяВременногоФайла,,Истина,retCode);
	Если retCode <> 0 Тогда
		Стр = "Не получилось добавить субтитры к файлу <" + ИмяФайлаШага + ">";
		Сообщить(Стр);
		ВызватьИсключение Стр;
	КонецЕсли;	
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяФайлаШага);
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяВременногоФайла);
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяФайлаСубтитров);
	ПараметрыВидео.Ванесса.ПереместитьФайлКомандаСистемы(ВременныйФайлВидео,ИмяФайлаШага);	
	
КонецПроцедуры

&НаКлиенте
Процедура СобратьВидеоВставку(ПараметрыВидео,ИмяФайлаШага,Шаг,НомерСценария,НомерШага)
	
	
	ТекстВставки = Шаг.ЗначенияПараметров[0].Значение.Значение;
	СтрИмяФайла = "break_scen" + ДобавитьНулей(НомерСценария,3) + "_step" + ДобавитьНулей(НомерШага,3);
	
	
	ФайлMp3 = ПолучитьФайлMP3ИзТекста(ПараметрыВидео,ТекстВставки,СтрИмяФайла);
	ДлительностьMp3 = Цел(ОпределитьДлительностьФайла(ПараметрыВидео,ФайлMp3)) + 1;
	
	
	ДлительностьВидео = 5;
	Если ДлительностьВидео < (ДлительностьMp3+1) Тогда
		ДлительностьВидео = ДлительностьMp3 + 1;
	КонецЕсли;	 
	
	
	КартинкаТекст = СоздатьКартинкуИзТекста(ПараметрыВидео,ТекстВставки,"caption",80);
	ФайлВидео     = СоздатьВидеоНужнойДлительностиИзКартинки(ПараметрыВидео,КартинкаТекст,ДлительностьВидео);
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(КартинкаТекст);
	
	
	
	ФайлТишины1 = СоздатьИмяФайлаТишины(ПараметрыВидео,1,СтрИмяФайла + "_silence_1");
	ФайлТишины2 = СоздатьИмяФайлаТишины(ПараметрыВидео,20,СтрИмяФайла + "_silence_2");
	ФайлMp3     = ОбъединитьФайлыMP3(ПараметрыВидео,ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\TTS",ФайлТишины1,ФайлMp3,ФайлТишины2);
	
	ДобавитьMp3КВидео(ПараметрыВидео,ФайлВидео,ФайлMp3);
	ФайлФайлВидео = Новый Файл(ФайлВидео);
	
	
	
	//ПараметрыВидео.Ванесса.ПереместитьФайлКомандаСистемы(ФайлВидео,ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + СтрИмяФайла + ФайлФайлВидео.Расширение);
	
	
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяФайлаШага);
	ПараметрыВидео.Ванесса.ПереместитьФайлКомандаСистемы(ФайлВидео,ИмяФайлаШага);	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьЗаписьВидео(ПараметрыВидео) Экспорт
	
	СоздатьIntro(ПараметрыВидео);
	СоздатьOutro(ПараметрыВидео);
	
	
	НомерСценария = -1;
	МассивСценариевДляВыполнения = ПараметрыВидео.МассивСценариевДляВыполнения;
	Для каждого Сценарий Из МассивСценариевДляВыполнения Цикл
		НомерСценария = НомерСценария + 1;
		Шаги = Сценарий.Шаги;
		
		ИмяСценария = Сценарий.Имя;
		
		
		СоздатьЗаголовокСценария(ПараметрыВидео,ИмяСценария,НомерСценария);
		
		НомерШага = -1;
		Для каждого Шаг Из Шаги Цикл
			НомерШага = НомерШага + 1;
			
			Если Шаг.ЭтоШагКонтекста Тогда
				Продолжить;
			КонецЕсли;	 
			
			
			ПараметрыВидео.Вставить("ТекИД_СценарияВМассиве",НомерСценария);
			ПараметрыВидео.Вставить("ТекИД_ШагаВМассиве",НомерШага);
			
			
			ИмяФайлаШага          = ПолучитьИмяФайлаВидеоШага(ПараметрыВидео);
			ФайлИмяФайлаВидеоШага = Новый Файл(ИмяФайлаШага);
			ИмяMp3Шага            = ФайлИмяФайлаВидеоШага.ИмяБезРасширения + ".mp3";//файл имеет такое же имя
			
			ИмяФайлаШага          = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\"    + ИмяФайлаШага;
			ИмяMp3Шага            = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\TTS\" + ИмяMp3Шага;
			
			Если ПараметрыВидео.Ванесса.ЭтоПрерывающйВидеоШаг(Шаг) Тогда
				СобратьВидеоВставку(ПараметрыВидео,ИмяФайлаШага,Шаг,НомерСценария,НомерШага);
			Иначе
				//1. надо масштабировать файл видео, чтобы он и звук правильно соотносились
				ДлительностьВидео = Цел(ОпределитьДлительностьФайла(ПараметрыВидео,ИмяФайлаШага));
				ДлительностьMP3   = Цел(ОпределитьДлительностьФайла(ПараметрыВидео,ИмяMp3Шага));
				
				Если ДлительностьВидео < (ДлительностьMP3 + 1) Тогда
					НоваяДлительностьВидео = ДлительностьMP3 + 1;
					УскоритьЗамедлитьФайлВидеоИДобавитьMp3(ПараметрыВидео,ИмяФайлаШага,ИмяMp3Шага,ДлительностьВидео,НоваяДлительностьВидео,ДлительностьMP3);
				ИначеЕсли ДлительностьВидео > (ДлительностьMP3 + 2) Тогда
					НоваяДлительностьВидео = ДлительностьMP3 + 2;
					УскоритьЗамедлитьФайлВидеоИДобавитьMp3(ПараметрыВидео,ИмяФайлаШага,ИмяMp3Шага,ДлительностьВидео,НоваяДлительностьВидео,ДлительностьMP3);
				Иначе
					УскоритьЗамедлитьФайлВидеоИДобавитьMp3(ПараметрыВидео,ИмяФайлаШага,ИмяMp3Шага,ДлительностьВидео,-1,ДлительностьMP3);//не меняем длительность
				КонецЕсли;	 
				
				ДобавитьСубтитрыКВидео(ПараметрыВидео,ИмяФайлаШага,Шаг,ДлительностьMP3);
			КонецЕсли;	 
			
		КонецЦикла;
		
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Функция РазложитьСтрокуВМассивПодстрок(Знач Строка, Знач Разделитель = ",", Знач ПропускатьПустыеСтроки = Неопределено)
	
	Результат = Новый Массив;
	
	// для обеспечения обратной совместимости
	Если ПропускатьПустыеСтроки = Неопределено Тогда
		ПропускатьПустыеСтроки = ?(Разделитель = " ", Истина, Ложь);
		Если ПустаяСтрока(Строка) Тогда 
			Если Разделитель = " " Тогда
				Результат.Добавить("");
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
		
	Позиция = Найти(Строка, Разделитель);
	Пока Позиция > 0 Цикл
		Подстрока = Лев(Строка, Позиция - 1);
		Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Подстрока) Тогда
			Результат.Добавить(Подстрока);
		КонецЕсли;
		Строка = Сред(Строка, Позиция + СтрДлина(Разделитель));
		Позиция = Найти(Строка, Разделитель);
	КонецЦикла;
	
	Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Строка) Тогда
		Результат.Добавить(Строка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции 

&НаКлиенте
Процедура ОбработатьСтрокуПоСловарю(ПараметрыВидео,ИсходнаяСтрока)
	Если СокрЛП(ПараметрыВидео.ЗаписьВидеоСловарьЗамен) = "" Тогда
		Возврат;
	КонецЕсли;	
	
	Если НЕ ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(ПараметрыВидео.ЗаписьВидеоСловарьЗамен) Тогда
		Сообщить("Не найден файл словаря замен! <" + ПараметрыВидео.ЗаписьВидеоСловарьЗамен + ">");
		ВызватьИсключение "Не найден файл словаря замен! <" + ПараметрыВидео.ЗаписьВидеоСловарьЗамен + ">";
	КонецЕсли;	 
	
	
	Текст = Новый ЧтениеТекста;
	Текст.Открыть(ПараметрыВидео.ЗаписьВидеоСловарьЗамен,"UTF-8");
	
	Пока Истина Цикл
		Стр = Текст.ПрочитатьСтроку();
		Если Стр = Неопределено Тогда
			Прервать;
		КонецЕсли;	 
		
		Если СокрЛП(Стр) = "" Тогда
			Продолжить;
		КонецЕсли;	 
		
		Массив = РазложитьСтрокуВМассивПодстрок(Стр,"|");
		
		ИсходнаяСтрока = СтрЗаменить(ИсходнаяСтрока,Массив[0],Массив[1]);
	КонецЦикла;	
	
	Текст.Закрыть();
КонецПроцедуры

&НаКлиенте
Функция ПолучитьВременныйФайлШаг(ПараметрыВидео,Знач Текст)
	ОбработатьСтрокуПоСловарю(ПараметрыВидео,Текст);
	
	Текст = СтрЗаменить(Текст,"\n","");
	Текст = СтрЗаменить(Текст,"\"," ");
	Текст = СтрЗаменить(Текст,"|"," ");
	Текст = Текст + ".";
	
	
	ИмяФайла = ПолучитьИмяВременногоФайла("txt");
	ЗТ = Новый ЗаписьТекста(ИмяФайла,"windows-1251",,Ложь); 
	ЗТ.ЗаписатьСтроку(Текст); 
	ЗТ.Закрыть();
	
	Возврат ИмяФайла;
КонецФункции	

&НаКлиенте
Функция ПолучитьФайлMP3ИзТекста(ПараметрыВидео,Текст,ИмяФайла)
	Если НЕ ПараметрыВидео.ЗаписьВидеоДелатьНаложениеTTS Тогда
		Возврат Неопределено;
	КонецЕсли;	 
	
	//получение wav
	ВременныйФайлШаг = ПолучитьВременныйФайлШаг(ПараметрыВидео,Текст);
	
	AudioEngine         = ПараметрыВидео.ЗаписьВидеоПутьКBalabolka_console;
	КаталогДляФайловTTS = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\TTS";
	ffmpeg              = ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg;
	
	
	ИмяWAV = КаталогДляФайловTTS + "\" + ИмяФайла + ".wav";
	//Команда = """" + AudioEngine + """ -f " + ВременныйФайлШаг + " -w " + ИмяWav +  " -n Elena > 111.txt 2>&1";
	Команда = """" + AudioEngine + """ -f " + ВременныйФайлШаг + " -w " + ИмяWAV +  " -fr 44 -bt 16 -ch 2 -s 2  -n " + ПараметрыВидео.ЗаписьВидеоИмяTTS;
	
	КомандаСистемы(Команда);
	
	Если НЕ ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(ИмяWAV) Тогда
		Стр = "Не получилось создать файл TTS <" + ИмяWAV + ">!";
		Сообщить(Стр);
		ВызватьИсключение Стр;
	КонецЕсли;	 
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ВременныйФайлШаг);
	
	
	
	//получение mp3
	ИмяMP3 = СтрЗаменить(ИмяWav,"wav","mp3");
	Команда = "" + ffmpeg + " -i " + ИмяWav + " -vn -ar 44100 -ac 2 -ab 192k -f mp3 -af ""volume=1.1"" " + ИмяMP3;
	//Сообщить(Команда);
	
	КомандаСистемы(Команда);
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяWav);
	
	Возврат ИмяMP3;
КонецФункции	

&НаКлиенте
Процедура СоздатьФайлыTTS(ПараметрыВидео) Экспорт
	
	Если НЕ ПараметрыВидео.ЗаписьВидеоДелатьНаложениеTTS Тогда
		Возврат;
	КонецЕсли;	 
	
	КаталогДляФайловTTS = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\TTS";
	Если НЕ ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(КаталогДляФайловTTS) Тогда
		ПараметрыВидео.Ванесса.СоздатьКаталогКомандаСистемы(КаталогДляФайловTTS);
	КонецЕсли;	 
	
	AudioEngine = ПараметрыВидео.ЗаписьВидеоПутьКBalabolka_console;
	Если НЕ ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(AudioEngine) Тогда
		Стр = "Программа для создания TTS не найдена! <" + AudioEngine +">";
		Сообщить(Стр);
		ВызватьИсключение Стр;
	КонецЕсли;
	
	Если СокрЛП(ПараметрыВидео.ЗаписьВидеоИмяTTS) = "" Тогда
		Стр = "Не указано имя TTS!";
		Сообщить(Стр);
		ВызватьИсключение Стр;
	КонецЕсли;
	
	
	ffmpeg = ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg;
	Если СокрЛП(ffmpeg) = "" Тогда
		Стр = "Не указана команда ffmpeg!";
		Сообщить(Стр);
		ВызватьИсключение Стр;
	КонецЕсли;	 
	
	НомерСценария = -1;
	МассивСценариевДляВыполнения = ПараметрыВидео.МассивСценариевДляВыполнения;
	Для каждого Сценарий Из МассивСценариевДляВыполнения Цикл
		НомерСценария = НомерСценария + 1;
		Шаги = Сценарий.Шаги;
		
		
		НомерШага = -1;
		Для каждого Шаг Из Шаги Цикл
			НомерШага = НомерШага + 1;
			Если Шаг.ЭтоШагКонтекста Тогда
				Продолжить;
			КонецЕсли;	 
			
			Текст = Шаг.Имя;
			
			
			ИмяMP3 = ПолучитьФайлMP3ИзТекста(ПараметрыВидео,Текст,"scen_" + ДобавитьНулей(НомерСценария,3) + "_step_" + ДобавитьНулей(НомерШага,3));
			
			
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция ДобавитьНулей(ЗначениеЧисло,КолНулей)
	Стр = СтрЗаменить(Строка(ЗначениеЧисло),Символы.НПП,"");
	
	Пока СтрДлина(Стр) < КолНулей Цикл
		Стр = "0" + Стр;
	КонецЦикла;
	
	Возврат Стр;
КонецФункции	

&НаКлиенте
Функция ПолучитьИмяФайлаВидеоШага(ПараметрыВидео)
	Стр = "scen_" + ДобавитьНулей(ПараметрыВидео.ТекИД_СценарияВМассиве,3) + "_step_" + ДобавитьНулей(ПараметрыВидео.ТекИД_ШагаВМассиве,3) + ".mp4";
	
	Возврат Стр;
КонецФункции	

&НаКлиенте
Процедура Инициализация(ПараметрыВидео) Экспорт
	
	Каталог = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов;
	Если НЕ ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(Каталог) Тогда
		Сообщить("Каталог ЗаписьВидеоКаталогДляВременныхФайлов <" + Каталог + "> не существует!");
		ВызватьИсключение "Каталог ЗаписьВидеоКаталогДляВременныхФайлов <" + Каталог + "> не существует!";
		Возврат;
	КонецЕсли;	 
	
	Если СокрЛП(ПараметрыВидео.ЗаписьВидеоКомандаConvert) = "" Тогда
		Стр = "Не указана команда Convert!";
		Сообщить(Стр);
		ВызватьИсключение Стр;
	КонецЕсли;	 
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗаписьВидеоОдногоШага(ПараметрыВидео) Экспорт
	КомандаНачатьЗаписьВидео = ПараметрыВидео.ЗаписьВидеоКомандаНачатьЗаписьВидео;
	
	Если ПараметрыВидео.ЗаписьВидеоПоказыватьКурсорМыши Тогда
		КомандаНачатьЗаписьВидео = СтрЗаменить(КомандаНачатьЗаписьВидео,"<screenmouseimage>",ПараметрыВидео.ЗаписьВидеоФайлКурсораМышки);
	Иначе
		КомандаНачатьЗаписьВидео = СтрЗаменить(КомандаНачатьЗаписьВидео,":screen-mouse-image=""<screenmouseimage>""","");
	КонецЕсли;	 
	
	
	КомандаНачатьЗаписьВидео = СтрЗаменить(КомандаНачатьЗаписьВидео,"<screenwidth>",ПараметрыВидео.ЗаписьВидеоЭкранШирина);
	КомандаНачатьЗаписьВидео = СтрЗаменить(КомандаНачатьЗаписьВидео,"<screenheight>",ПараметрыВидео.ЗаписьВидеоЭкранВысота);
	
	
	
	ИмяФайлаВидео            = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов  + "\" + ПолучитьИмяФайлаВидеоШага(ПараметрыВидео);
	КомандаНачатьЗаписьВидео = СтрЗаменить(КомандаНачатьЗаписьВидео,"<dst>",ИмяФайлаВидео);
	
	
	КолКадров = ПараметрыВидео.ЗаписьВидеоКоличествоКадров;
	Если КолКадров = 0 Тогда
		ВызватьИсключение "Не указано количество кадров!";
	КонецЕсли;	 
	КомандаНачатьЗаписьВидео = СтрЗаменить(КомандаНачатьЗаписьВидео,"<fps>",Строка(КолКадров));
	
	КомандаНачатьЗаписьВидео = СтрЗаменить(КомандаНачатьЗаписьВидео,Символы.НПП,"");
	
	ПараметрыВидео.Ванесса.Отладка(КомандаНачатьЗаписьВидео);
	
	
	ПараметрыВидео.Ванесса.ВыполнитьКомандуОСБезПоказаЧерногоОкна(КомандаНачатьЗаписьВидео,0);
	
	
	//ждём чтобы файл появился на диске
	СчетчикПроверок     = 0;
	МаксСчетчикПроверок = 10;
	Пока Истина Цикл
		СчетчикПроверок = СчетчикПроверок + 1;
		
		Если СчетчикПроверок > МаксСчетчикПроверок Тогда
			ВызватьИсключение "Не найден файл <" + ИмяФайлаВидео + ">";
		КонецЕсли;	 
		
		
		Если НЕ ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(ИмяФайлаВидео) Тогда
			ПараметрыВидео.Ванесса.Sleep(1);
			Продолжить;
		Иначе
			Прервать;
		КонецЕсли;	 
		
		
	КонецЦикла;
	
	
КонецПроцедуры


&НаКлиенте
Функция ПолучитьАдресСокета(Знач Стр)
	Поз = Найти(Стр,"--rc-host=");
	Стр = Сред(Стр,Поз);
	Стр = СтрЗаменить(Стр,"--rc-host=","");
	
	Поз = Найти(Стр," ");
	Стр = Лев(Стр,Поз-1);
	Стр = СокрЛП(Стр);
	
	Возврат Стр;
КонецФункции	

&НаКлиенте
Процедура ОстановитьЗаписьВидеоОдногоШага(ПараметрыВидео) Экспорт
	АдресСокета = ПолучитьАдресСокета(ПараметрыВидео.ЗаписьВидеоКомандаНачатьЗаписьВидео);
	
	КомандаОстановитьЗапись = "oscript " + ПараметрыВидео.КаталогИнструментов + "\tools\onescript\StopVideoRec.os """ + АдресСокета + """";
	
	ПараметрыВидео.Ванесса.Отладка("КомандаОстановитьЗапись=" + КомандаОстановитьЗапись);
	
	Рез = ПараметрыВидео.Ванесса.ВыполнитьКомандуОСБезПоказаЧерногоОкна(КомандаОстановитьЗапись);
	Если Рез <> 0 Тогда
		Сообщить("Команда ОстановитьЗаписьВидео вернула код возврата = " + Рез);
		ВызватьИсключение "Команда ОстановитьЗаписьВидео вернула код возврата = " + Рез;
	КонецЕсли;	 
	
КонецПроцедуры